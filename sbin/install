#!/usr/bin/env raku

use Test;

use GNU::FreeFont-OTF::Vars;

if not @*ARGS {
    print qq:to/HERE/;
    Usage: {$*PROGRAM.basename} go

    Used by the Build.rakudoc module
    to insert the currently available
    language samples into the docs.
    HERE
    exit;
}

# Reads the docs/README.rakudoc file and
# ensures the list of installed 
# languages is correct. The
# source of truth is in the 
# sample fonts list in file:
#   /lib/*/*/Vars.rakumod

my $debug = 0;

for @*ARGS {
    when /^ :i d/ { ++$debug }
}

if  $*HOME ~~ /tbrowde/ {
    # only works on the author's servers
    say "DEBUG: on the author's servers" if $debug;
}
else { exit }

say "DEBUG is on" if $debug;

my $docfile = "$*CWD/docs/README.rakudoc";
my @lines   = $docfile.IO.lines;
unless @lines.elems {
    say "DEBUG: Unable to read any line from file: $docfile";
    say "       Exiting...";
    exit;
}

my $in-list = False;

# First see if all is well with the current docs
# and lang code and name match

# The known data to check against:
my %samp = %default-samples;
# All we need are the iso and lang name
my %h1;
for %samp.keys -> $iso-key {
    my $lang = %samp{$iso-key}<lang>;
    %h1{$iso-key} = $lang;
}

my $rewrite = False;

my %h2; # this docs's lang hash
LINE: for @lines -> $line is copy {
    #say "DEBUG: doc line: |$line|" if $debug;

    if $line  ~~ /^ \h* Z :i \< \h* begin \h* lang \h* list \h* \> / {
        $in-list = True;
        say "DEBUG: in the lang list" if $debug;
        next LINE;
    }
    if $line ~~ /^ \h* Z :i \< \h* end \h* lang \h* list \h* \> / {
        $in-list = False;
        say "DEBUG: exited the lang list" if $debug;
    }

    next LINE unless $in-list;

    # A data line:
    # =item uk - Ukranian
    if $line ~~ /^ \h* '=item' \h+ (\w\w)  \h+ '-' \h+ (\N+) $/ {
        # ok
        my $iso-code = ~$0.trim;
        my $lang     = ~$1.trim;
        if %h2{$iso-code}:exists {
            die "FATAL: Duplicate ISO code $iso-code in file $docfile";
        }
        else {
            %h2{$iso-code} = $lang;
        }
        say "DEBUG: good format: |$line|" if $debug;
    }
    else {
        die qq:to/HERE/;
        FATAL: Unexpected line in file '{$docfile}':
                  |$line|
               Exiting...
        HERE
    }
}

unless is-deeply %h1, %h2 {
    note qq:to/HERE/;
    FATAL: The language list in $docfile is NOT 
           identical to the source in file:
               $docfile
    HERE
    exit(1);
}

# if it's not current, update it
# my $fh = open $docfil, :w;
